#pragma kernel UpdateTrailLodBuffer NUM_THREAD_X=32
#pragma kernel UpdateTrailIndexBuffer NUM_THREAD_X=32

#pragma multi_compile_local __ GPUTRAIL_TRAIL_INDEX_ON

#include "GpuTrailCSInclude.cginc"
#include "GpuTrailIndexInclude.cginc"


////////////////////////////////////////////////////////////////////////////////
// UpdateTrailIdxBuffer
////////////////////////////////////////////////////////////////////////////////
float3 _CameraPos;
StructuredBuffer<Node> _NodeBuffer;
StructuredBuffer<float> _LodDistanceBuffer; // Order By distance
RWStructuredBuffer<uint> _TrailLodBufferW;

[numthreads(NUM_THREAD_X,1,1)]
void UpdateTrailLodBuffer (uint3 id : SV_DispatchThreadID)
{
	uint trailIdxBufferIdx = id.x;
	if ( trailIdxBufferIdx < GetTrailNum() )
	{
		uint trailIdx = GetTrailIdx(trailIdxBufferIdx);

		float minDistanceSq = -1.0;

		uint nodeIdxStart = calcNodeIdx(trailIdx, 0);
		uint nodeIdxEnd = nodeIdxStart + _NodeNumPerTrail;
		for(uint nodeIdx = nodeIdxStart; nodeIdx < nodeIdxEnd; nodeIdx++)
		{
			Node node = _NodeBuffer[nodeIdx];
			if ( node.time > 0)
			{
				float3 posFromCamera = node.pos - _CameraPos;
				float distanceSq = dot(posFromCamera, posFromCamera);

				minDistanceSq = (minDistanceSq<0) ? distanceSq : min(minDistanceSq, distanceSq);
			}
		}
	

		float minDistance = sqrt(minDistanceSq);

		uint num, stride;
		_LodDistanceBuffer.GetDimensions(num, stride);

		for(uint nextLod=1; nextLod<num; ++nextLod)
		{
			if (minDistance < _LodDistanceBuffer[nextLod])
			{
				break;
			}
		}

		_TrailLodBufferW[trailIdx] = nextLod-1;
	}
}


////////////////////////////////////////////////////////////////////////////////
// UpdateTrailIdxBuffer
////////////////////////////////////////////////////////////////////////////////
uint _CurrentLod;
StructuredBuffer<uint> _TrailLodBuffer;
AppendStructuredBuffer<uint> _TrailIdxBufferAppend;

[numthreads(NUM_THREAD_X,1,1)]
void  UpdateTrailIndexBuffer (uint3 id : SV_DispatchThreadID)
{
	uint trailIdxBufferIdx = id.x;
	if ( trailIdxBufferIdx < GetTrailNum() )
	{
		uint trailIdx = GetTrailIdx(trailIdxBufferIdx);
		if (_TrailLodBuffer[trailIdx] == _CurrentLod)
		{
			_TrailIdxBufferAppend.Append(trailIdx);
		}
	}
}