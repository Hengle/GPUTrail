#pragma kernel AppendNode NUM_THREAD_X=32

#include "GpuTrailVariablesCS.compute"

StructuredBuffer<float3> _InputBuffer_Pos;
StructuredBuffer<float4> _InputBuffer_Color;
RWStructuredBuffer<Trail> _TrailBuffer;
RWStructuredBuffer<Node> _NodeBuffer;

[numthreads(NUM_THREAD_X,1,1)]
void AppendNode (uint3 id : SV_DispatchThreadID)
{
	uint trailIdx = id.x;
	if ( trailIdx < _TrailNum )
	{
		Trail trail = _TrailBuffer[trailIdx];
		uint totalInputNum = trail.totalInputNum;
		bool isFirst = (totalInputNum==0);

		float3 inputPos = _InputBuffer_Pos[trailIdx];
		if ( isFirst || (_MinNodeDistance <= distance(inputPos, _NodeBuffer[calcNodeIdx(trailIdx, totalInputNum-1)].pos)))
		{
			Node node;
			node.pos = inputPos;
			node.time = _Time;
			node.color = _InputBuffer_Color[trailIdx];
			uint nodeIdx = calcNodeIdx(trailIdx, totalInputNum);
			_NodeBuffer[nodeIdx] = node;

			trail.startTime = isFirst ? _Time : trail.startTime;
			trail.totalInputNum++;

			_TrailBuffer[trailIdx] = trail;
		}
	}
}